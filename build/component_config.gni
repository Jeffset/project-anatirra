template("component_config") {
  component_name_ = invoker.name

  include_dir_ = "$target_gen_dir/include"

  include_config_name_ = "${target_name}_include_config"
  config(include_config_name_) {
    include_dirs = [
      include_dir_,
    ]
  }

  impl_define_config_name_ = "${target_name}_impl_define_config"
  config(impl_define_config_name_) {
    defines = ["COMPONENT_${component_name_}_BUILD"]
  }

  action(target_name) {
    # Make this visible only for component target.
    visibility = [":$component_name_"]

    script = "//build/scripts/component_config.py"
    output_ = "$include_dir_/${component_name_}_config.hpp"
    outputs = [
      output_,
    ]
    args = [
      "--name", component_name_,
      "--output", rebase_path(output_),
      "--params",
    ]

    foreach(param, invoker.params) {
      args += [ param ]
    }

    all_dependent_configs = [ ":$include_config_name_" ]
    public_configs = [ ":$impl_define_config_name_" ]
  }
}

template("component") {
 if (is_debug) {
   shared_library(target_name) {
     forward_variables_from(invoker, "*")
   }
 } else {
   static_library(target_name) {
     forward_variables_from(invoker, "*")
   }
 }
}
