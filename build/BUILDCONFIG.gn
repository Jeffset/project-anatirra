declare_args() {
  is_debug = true
  use_gcc = false
}

if (target_os == "") {
  target_os = host_os
}
if (target_cpu == "") {
  target_cpu = host_cpu
}
if (current_cpu == "") {
  current_cpu = target_cpu
}
if (current_os == "") {
  current_os = target_os
}

# All binary targets will get this list of configs by default.
_shared_binary_target_configs = [
  "//build:compiler_defaults",
  "//build:death_march_mode",
  "//build:default_include_dirs",
  "//build:complete_library",
]

if (!use_gcc) {
 _shared_binary_target_configs += [ "//build:death_march_mode_clang" ]
}

if (is_debug) {
  _shared_binary_target_configs += [ "//build:debug_config" ]
} else {
  _shared_binary_target_configs += [ "//build:release_config" ]
}

# Apply that default list to the binary target types.
set_defaults("executable") {
  configs = _shared_binary_target_configs

  # Executables get this additional configuration.
  configs += [ "//build:executable_ldconfig" ]

  if (is_debug) {
   configs += [ "//build:executable_debug_ldconfig" ]
  }
}
set_defaults("static_library") {
  configs = _shared_binary_target_configs
}
set_defaults("shared_library") {
  configs = _shared_binary_target_configs
}
set_defaults("source_set") {
  configs = _shared_binary_target_configs
}

if (use_gcc) {
  set_default_toolchain("//build/toolchain:linux_gcc")
} else {
  set_default_toolchain("//build/toolchain:linux_clang")
}
