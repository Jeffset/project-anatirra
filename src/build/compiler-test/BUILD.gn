toolchain("clang_compiler_test") {
  toolchain_args = {
    is_compiler_test = true
  }

  _runner_source = "//build/scripts/compiler-test.py"
  _runner = "python3 " + rebase_path(_runner_source)

  tool("cxx") {
    depfile = "{{output}}.d"
    _compiler_command_line = "-MF $depfile {{defines}} {{include_dirs}} {{cflags}} {{cflags_cc}} -c {{source}} -o {{output}}"
    command = "$_runner clang++ $_compiler_command_line"
    description = "TEST-CXX {{output}}"
    outputs = [
      "{{source_out_dir}}/{{source_name_part}}.o.py",
    ]
  }

  tool("stamp") {
    command = "touch {{output}}"
    description = "STAMP {{output}}"
  }
}
