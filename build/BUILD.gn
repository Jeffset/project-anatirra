config("compiler_defaults") {
  if (current_os == "linux") {
    cflags = [
      "-pipe",
      "-fPIC",
      "-pthread",
    ]
    cflags_cc = [
      "-std=c++17",
      "-fno-rtti",
    ]
    ldflags = [ "-fuse-ld=lld" ]
  }
}

config("death_march_mode") {
  cflags = [
    "-Wall",
    "-Wextra",
    "-Wpedantic",
  ]

  # TODO: These are probably specific to clang.
  warnings_as_errors = [
    "inconsistent-missing-override",
    "pessimizing-move",
    "return-type",
  ]

  foreach(warning, warnings_as_errors) {
    cflags += [ "-Werror=$warning" ]
  }
}

config("debug_config") {
  cflags = [
    "-g",
    "-O0",
    "-fasynchronous-unwind-tables",
  ]
  defines = [ "DEBUG" ]
}

config("release_config") {
  cflags = [ "-O2" ]
  defines = [ "NDEBUG" ]
  ldflags = [ "-use-lto" ]
}

config("default_include_dirs") {
  include_dirs = [ "//" ]
}

config("complete_library") {
  ldflags = [ "-Wl,-no-undefined" ]
}

config("executable_ldconfig") {
  ldflags = [
    "-Wl,-rpath=\$ORIGIN/",
    "-Wl,-rpath-link=",
  ]
}

config("executable_debug_ldconfig") {
  ldflags = [
    "-Wl,-export-dynamic",
  ]
}
