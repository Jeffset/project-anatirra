config("compiler_defaults") {
  if (current_os == "linux") {
    cflags = [
      "-pipe",
      "-pthread",

      "-fvisibility=hidden",
      "-fvisibility-inlines-hidden",
    ]
    cflags_cc = [
      "-std=c++17",
      "-fno-rtti",
      "-march=native",
    ]
    if (use_gcc) {
      ldflags = [ "-fuse-ld=gold" ]
    } else {
      ldflags = [ "-fuse-ld=lld" ]
    }
  }
}

config("shared_library_config") {
 cflags = [
   "-fPIC",
 ]
}

config("death_march_mode") {
  cflags = [
    "-Wall",
    "-Wextra",
    "-Wpedantic",
  ]
}

config("death_march_mode_clang") {
  # TODO: These are probably specific to clang.
  warnings_as_errors = [
    "inconsistent-missing-override",
    "pessimizing-move",
    "return-type",
  ]

  cflags = []
  foreach(warning, warnings_as_errors) {
    cflags += [ "-Werror=$warning" ]
  }
}

config("debug_config") {
  cflags = [
    "-g",
    "-O0",
    "-fasynchronous-unwind-tables",
  ]
  defines = [ "DEBUG" ]
}

config("release_config") {
  cflags = [
    "-g0",
    "-O3",
    "-flto",
    "-fno-asynchronous-unwind-tables",
    "-ffunction-sections",
    "-fdata-sections",
  ]
  defines = [ "NDEBUG" ]
  ldflags = [ "-Wl,--gc-sections" ]
}

config("default_include_dirs") {
  include_dirs = [ "//" ]
}

config("complete_library") {
  ldflags = [ "-Wl,-no-undefined" ]
}

config("executable_ldconfig") {
  ldflags = [
    "-Wl,-rpath=\$ORIGIN/",
    "-Wl,-rpath-link=",
  ]
}

config("executable_debug_ldconfig") {
  ldflags = [
    "-Wl,-export-dynamic",
  ]
}
