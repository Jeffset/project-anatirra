import("//build/pkg_config.gni")

declare_args() {
  use_tickit = false
}

pkg_config("backend") {
  if (use_tickit) {
    packages = [ "tickit" ]
  } else {
    packages = [ "ncursesw" ]
  }
}

shared_library("cursedui") {
  if (use_tickit) {
    _backend = "tickit"
  } else {
    _backend = "ncurses"
  }

  sources = [
    "$_backend/context.cc",
    "${_backend}/rendering.cc",
    "context.hpp",
    "dim.cc",
    "dim.hpp",
    "linear_layout.cc",
    "linear_layout.hpp",
    "rendering.hpp",
    "text_view.cc",
    "text_view.hpp",
    "util.hpp",
    "view.cc",
    "view.hpp",
    "view_group.cc",
    "view_group.hpp",
    "view_root.cc",
    "view_root.hpp",
    "view_specs.cc",
    "view_specs.hpp",
    "drawable.cc",
    "drawable.hpp",
  ]

  configs += [ ":backend" ]

  deps = [
    "//base",
  ]
}

executable("cursedui_test_bin") {
  sources = [
    "main.cc",
  ]
  deps = [
    ":cursedui",
    "//base",
  ]
}
