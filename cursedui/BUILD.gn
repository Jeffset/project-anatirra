import("//build/pkg_config.gni")

declare_args() {
  use_tickit = false
}

pkg_config("backend") {
  if (use_tickit) {
    packages = [ "tickit" ]
  } else {
    packages = [ "ncursesw" ]
  }
}

if (use_tickit) {
  config("backend_config") {
    defines = [ "CURSEDUI_USE_TICKIT" ]
  }
} else {
  config("backend_config") {
    defines = [ "CURSEDUI_USE_NCURSES" ]
  }
}

shared_library("cursedui") {
  if (use_tickit) {
    _backend = "tickit"
  } else {
    _backend = "ncurses"
  }

  sources = [
    "$_backend/context.cc",
    "$_backend/rendering.cc",
    "color.hpp",
    "context.hpp",
    "dim.cc",
    "dim.hpp",
    "drawable.cc",
    "drawable.hpp",
    "linear_layout.cc",
    "linear_layout.hpp",
    "rendering.hpp",
    "text_view.cc",
    "text_view.hpp",
    "util.hpp",
    "view.cc",
    "view.hpp",
    "view_group.cc",
    "view_group.hpp",
    "view_root.cc",
    "view_root.hpp",
    "view_specs.cc",
    "view_specs.hpp",
  ]

  configs += [ ":backend" ]
  all_dependent_configs = [ ":backend_config" ]

  deps = [
    "//base",
  ]
}

executable("cursedui_test_bin") {
  sources = [
    "main.cc",
  ]
  deps = [
    ":cursedui",
    "//base",
  ]
}
